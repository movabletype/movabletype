#!/usr/bin/env perl -w

use strict;
use warnings;
use lib 'lib';
use lib 'extlib';
use open qw(:utf8);
use File::Spec;
use Encode;
use MT;
use MT::Util qw(encode_js);

my $admin_theme_id = 'admin2023/';

my @files = (
    "mt-static/mt.js",
    "mt-static/jquery/jquery.mt.js",
    "mt-static/js/dialog.js",
    "mt-static/js/assetdetail.js",
    "mt-static/js/upload_settings.js",
    "mt-static/js/edit.js",
    "mt-static/archetype/js/archetype_editor.js",
    "mt-static/js/cms.js",

    glob("mt-static/js/admin2023/contenttype/tag/*.tag"),
    glob("src/contenttype/elements/*.svelte"),
    glob("mt-static/js/admin2023/listing/tag/*.tag"),
    glob("src/listing/elements/*.svelte"),
    "mt-static/js/mt/util.js",
    "mt-static/plugins/BlockEditor/lib/js/blockeditor_field.js",
    "mt-static/plugins/BlockEditor/lib/js/blockeditor_field_manager.js",
    "mt-static/plugins/BlockEditor/lib/js/fields/embed.js",
    "mt-static/plugins/BlockEditor/lib/js/fields/header.js",
    "mt-static/plugins/BlockEditor/lib/js/fields/horizon.js",
    "mt-static/plugins/BlockEditor/lib/js/fields/image.js",
    "mt-static/plugins/BlockEditor/lib/js/fields/text.js",
    "mt-static/plugins/BlockEditor/lib/js/jquery.blockeditor.js",
    "mt-static/plugins/BlockEditor/lib/js/modal_window.js",
    "mt-static/plugins/FormattedTextForTinyMCE6/extension.js",
    "mt-static/plugins/TinyMCE6/lib/js/tinymce/plugins/mt/plugin.js",
    "mt-static/plugins/TinyMCE6/lib/js/tinymce/plugins/mt/langs/plugin.js",
    "mt-static/plugins/TinyMCE6/lib/js/tinymce/plugins/mt_fullscreen/plugin.js",
    "mt-static/plugins/TinyMCE6/lib/js/tinymce/plugins/mt_fullscreen/langs/plugin.js",
    "plugins/BlockEditor/tmpl/${admin_theme_id}cms/dialog/asset_modal.tmpl",
    "plugins/BlockEditor/tmpl/${admin_theme_id}cms/dialog/include/asset_upload_panel.tmpl",
    "plugins/BlockEditor/tmpl/${admin_theme_id}cms/field_html/field_html_multi_line_text.tmpl",
    "plugins/FormattedText/tmpl/cms/edit_formatted_text.tmpl",
    "tmpl/${admin_theme_id}cms/edit_commenter.tmpl",
    "tmpl/${admin_theme_id}cms/edit_content_data.tmpl",
    "tmpl/${admin_theme_id}cms/edit_content_type.tmpl",
    "tmpl/${admin_theme_id}cms/edit_entry.tmpl",
    "tmpl/${admin_theme_id}cms/edit_template.tmpl",
    "tmpl/${admin_theme_id}cms/field_html/field_html_asset.tmpl",
    "tmpl/${admin_theme_id}cms/field_html/field_html_categories.tmpl",
    "tmpl/${admin_theme_id}cms/field_html/field_html_content_type.tmpl",
    "tmpl/${admin_theme_id}cms/field_html/field_html_multi_line_text.tmpl",
    "tmpl/${admin_theme_id}cms/include/asset_upload.tmpl",
    "tmpl/${admin_theme_id}cms/include/async_asset_list.tmpl",
    "tmpl/${admin_theme_id}cms/include/async_asset_upload.tmpl",
    "tmpl/${admin_theme_id}cms/include/category_selector.tmpl",
    "tmpl/${admin_theme_id}cms/include/content_data_list.tmpl",
    "tmpl/${admin_theme_id}cms/list_category.tmpl",
    "tmpl/${admin_theme_id}cms/listing/tag_list_header.tmpl",
);

my $addon_path = File::Spec->catdir('addons');
local *DH;
my @addons;
if ( opendir my $dh, $addon_path ) {
    my @p = readdir $dh;
    foreach my $p (@p) {
        next if $p eq '.' || $p eq '..';
        my $full_path = File::Spec->catdir( $addon_path, $p );
        if ( -d $full_path ) {
            if ( $p =~ m/^(.+)\.(\w+)$/ ) {
                my $label = $1;
                my $id    = lc $1;
                my $type  = $2;
                next if $type ne 'pack';
                if ( open my $fh,
                    '<',
                    File::Spec->catfile( $full_path, 'make_js_templates' ) )
                {
                    while (<$fh>) {
                        chomp $_;
                        push @files, $_;
                    }
                    close $fh;
                    push @addons, $label;
                }
            }
        }
    }
    closedir $dh;
}

my $plugin_path = File::Spec->catdir('plugins');
my @plugins;
if ( opendir my $dh, $plugin_path ) {
    my @p = readdir $dh;
    foreach my $p (@p) {
        next if $p eq '.' || $p eq '..';
        my $full_path = File::Spec->catdir( $plugin_path, $p );
        if ( -d $full_path ) {
            push @plugins, $p;
        }
    }
    closedir $dh;
}

my $js = '';
for my $file (@files) {
    open my $fh, '<', $file or die "$file: $!";
    local $/;
    $js .= <$fh>;
}

foreach my $lang (qw(en_us ja)) {
    eval 'require MT::L10N::' . $lang
        or die "failed to find package MT::L10N::$lang";

    my $lex = eval '\%{ MT::L10N::' . $lang . '::Lexicon }' || {};

    foreach my $addon (@addons) {
        eval {
            unshift @INC, "addons/$addon.pack/lib";
            require "./addons/$addon.pack/lib/MT/$addon/L10N/$lang.pm";
        };
        next if $@;
        my $conv;
        $conv = eval '\%{MT::' . $addon . '::L10N::' . $lang . '::Lexicon}';
        if (%$conv) {
            $lex = { %$lex, %$conv, };
        }
    }

    foreach my $plugin (@plugins) {
        eval {
            unshift @INC, "plugins/$plugin/lib";
            require "./plugins/$plugin/lib/MT/$plugin/L10N/$lang.pm";
        };
        next if $@;
        my $conv;
        $conv = eval '\%{MT::' . $plugin . '::L10N::' . $lang . '::Lexicon}';
        if (%$conv) {
            $lex = { %$lex, %$conv, };
        }
    }

    my $enc_js = Encode::encode_utf8($js);
    $enc_js =~ s/\\'/\t\t/g;
    my %lexicon;
    while ( $enc_js =~ m/trans\(\s*(["'])([^\1|\\\1]+?)\1/g ) {
        my $str = $2;
        $str =~ s/\t\t/'/g;
        my $local = $lex->{$str} ? Encode::encode_utf8( $lex->{$str} ) : $str;
        $str   = encode_js($str);
        $local = encode_js($local);
        next if $str eq $local;
        $str = Encode::decode_utf8($str);
        $local = Encode::decode_utf8($local) unless Encode::is_utf8($local);
        $lexicon{$str} = $local;
    }

    open( my $FOUT, ">", "./mt-static/mt_$lang.js" )
        or die "failed to open mt-static/mt_$lang.js for writing";

    binmode $FOUT, ":utf8";
    print $FOUT
        "/* Movable Type language lexicon for $lang localization. */\n\n";

    for my $text ( sort keys %lexicon ) {
        print $FOUT "Lexicon['$text'] = '$lexicon{$text}';\n";
    }
    close $FOUT;
}
