#!/usr/bin/perl -w
use strict;
use warnings;
use utf8;
use lib "lib", "extlib";
use Getopt::Long;
use MT::Bootstrap;
use MT;
use MT::Revisable;

my %target;

GetOptions(
    'b|blog_id=i' => \my ($blog_id),
    'd|delete'   => \my ($delete),
    'e|entry'    => sub { $target{entry}    = 1 },
    'c|cd'       => sub { $target{cd}       = 1 },
    't|template' => sub { $target{template} = 1 },
    "help|?"     => sub { usage(); exit },
);

my $delete_count = 0;
my $mt           = MT->new;

my $site_iter = MT::Blog->load_iter($blog_id ? { id => $blog_id } : { class => '*' });

while ( my $site = $site_iter->() ) {

    for my $ds ( keys %target ) {

        my $model = MT->model($ds);
        my $col   = 'max_revisions_' . $ds;
        my $max   = $site->$col || $MT::Revisable::MAX_REVISIONS;

        print "============================================================\n";
        printf( "Checking %s of site(id:%s) for max=%d\n", $ds, $site->id, $max );
        print "============================================================\n";

        my $obj_iter  = $model->load_iter( { blog_id => $site->id } );
        my $rev_class = MT->model( $ds . ':revision' );
        my $found     = 0;

        while (my $obj = $obj_iter->()) {
            my $terms = { $ds . '_id' => $obj->id };
            my $count = $rev_class->count($terms);
            if ($count > $max) {
                $delete_count += ($count - $max);
                print sprintf(
                    "Violation detected and deleting (%d/%d) for id=%d\n",
                    $count, $max, $obj->id
                );
                if ($delete) {
                    my $plan = $count - $max;
                    while ($plan > 0) {
                        my $limit = $plan > 100 ? 100 : $plan;
                        $plan -= $limit;
                        my @ids = map { $_->id } $rev_class->load(
                            $terms,
                            {
                                fetchonly => { 'id' => 1 }, sort => 'created_on', direction => 'ascend',
                                limit     => $limit
                            });
                        my $res = $rev_class->remove({ id => \@ids }, { nofetch => 1 });
                    };
                }
                $found = 1;
            }
        }
        print( !$found ? "OK.\n\n" : $delete ? "Fixed.\n\n" : "\n" );
    }
}

print "============================================================\n";
print "Total Result\n";
printf( "%s: %d\n", ( $delete ? 'Deleted' : 'Detected' ), $delete_count );
print "============================================================\n";

sub usage {
    print STDERR << "EOT";
usage: $0

These options are available:

-b, --blog_id           Blog ID.
-d, --delete            Delete on detecting revision amount excession.
-e, --entry             process entries.
-c, --cd                process content data.
-t, --template          process templates.
EOT
}
